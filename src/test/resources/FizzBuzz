
@Z = \f . (\x . f(\y . x(x)(y) ) )(\x . f(\y . x(x)(y) ) ) 

@ZERO  = \f . \x . x 
@ONE   = \f . \x . f(x) 
@THREE = \f . \x . f(f(f(x))) 
@FIVE  = \f . \x . f(f(f(f(f(x))))) 
@NINE  = \f . \x . f(f(f(f(f(f(f(f(f(x))))))))) 
@TEN   = \f . \x . f(f(f(f(f(f(f(f(f(f(x)))))))))) 

@IS_ZERO = \n . n(\x . FALSE )(TRUE) 

@INC = \n . \f . \x . f(n(f)(x)) 
@DEC = \n . LEFT(n(\p . PAIR(RIGHT(p))(INC(RIGHT(p))) )(PAIR(ZERO)(ZERO))) 
@ADD = \m . \n . \f . \x . n(f)(m(f)(x)) 
@SUB = \m . \n . n(DEC)(m) 
@MUL = \m . \n . n(ADD(m))(ZERO) 
@DIV = Z(\f . \m . \n . IF(IS_LESS_OR_EQUAL(n)(m))(\x . INC(f(SUB(m)(n))(n))(x) )(ZERO) )
@MOD = Z(\f . \m . \n . IF(IS_LESS_OR_EQUAL(n)(m))(\x . f(SUB(m)(n))(n)(x) )(m) )

@IS_LESS_OR_EQUAL = \m . \n . IS_ZERO(SUB(m)(n)) 

@FIFTEEN = MUL(THREE)(FIVE)
@HUNDRED = MUL(TEN)(TEN)

@TRUE  = \x . \y . x 
@FALSE = \x . \y . y 
@IF = \c . \x . \y . c(x)(y) 

@PAIR  = \x . \y . \f . f(x)(y) 
@LEFT  = \p . p(TRUE) 
@RIGHT = \p . p(FALSE) 

@NIL  = PAIR(PAIR(TRUE)(TRUE))(TRUE)
@CONS = \h . \t . PAIR(PAIR(FALSE)(h))(t) 
@HEAD = \l . RIGHT(LEFT(l)) 
@TAIL = \l . RIGHT(l) 
@IS_NIL = \l . LEFT(LEFT(l)) 
@RANGE = Z(\f . \x . \y . IF(IS_ZERO(SUB(y)(x)))(CONS(x)(NIL))(CONS(x)(\z . f(INC(x))(y)(z) )) )
@MAP = Z(\f . \g . \l . IF(IS_NIL(l))(NIL)(\x . CONS(g(HEAD(l)))(f(g)(TAIL(l)))(x) ) )

@F = TEN
@B = INC(F)
@I = INC(B)
@U = INC(I)
@ZZ = INC(U)
@FIZZ = CONS(F)(CONS(I)(CONS(ZZ)(CONS(ZZ)(NIL))))
@BUZZ = CONS(B)(CONS(U)(CONS(ZZ)(CONS(ZZ)(NIL))))
@FIZZBUZZ = CONS(F)(CONS(I)(CONS(ZZ)(CONS(ZZ)(CONS(B)(CONS(U)(CONS(ZZ)(CONS(ZZ)(NIL))))))))

@TO_STR_SUPPORT = Z(\f . \n . \l . IF(IS_LESS_OR_EQUAL(n)(NINE))(CONS(n)(l))(\x . f(DIV(n)(TEN))(CONS(MOD(n)(TEN))(l))(x) ) )
@TO_STR = \n . TO_STR_SUPPORT(n)(NIL) 

@RET = MAP(\a . IF(IS_ZERO(MOD(a)(FIFTEEN)))(FIZZBUZZ)(
                 IF(IS_ZERO(MOD(a)(THREE)))(FIZZ)(
                 IF(IS_ZERO(MOD(a)(FIVE)))(BUZZ)(
                 TO_STR(a))))
          )(RANGE(ONE)(HUNDRED)) 
